10     LET A=0
20     INPUT A
30     IF A=1 GOTO 290 :  REM COIN TEST
40     IF A=2 GOTO 420 :  REM COLLECT
50     IF A=3 GOTO 490 :  REM REFUND
60     IF A=4 GOTO 680 :  REM RESULT
70     IF A=5 GOTO 560 :  REM OPERATOR ATTACHED
80     IF A=6 GOTO 640 :  REM OPERATOR DETATCHED
90     IF A=9 GOTO 180 :  REM RESET
100    GOTO 10
110    REM CLEANUP ROUTINE
120    CLEAR S
130    LET X=0 :  REM SLOW THIS THING DOWN A BIT
140    DO
150   X=X+1
160    WHILE X<250 :  REM NO SLEEP FUNCTION SO
170    IF O=1 GOTO 240 :  REM IF OPERATOR ATTACHED PRESERVE THAT
180    LET O=0 :  REM RESET OPR ATTACHED BIT
190    PUSH 0 :  REM RESET EVERYTHING TO 0
200    PUSH 201
210    CALL 25
220    CALL 54
230    GOTO 10 :  REM BACK TO THE MENU
240    PUSH 8 :  REM OPERATOR ATTACHED SO 8
250    PUSH 201
260    CALL 25
270    CALL 54
280    GOTO 10 : BACK TO THEBEGINNING
290    REM COIN TEST
300    CLEAR S
310    PUSH 1 :  REM COIN TEST IS 0001
320    PUSH 201
330    CALL 25
340    CALL 54
350    LET Y=0 :  REM WE ARE ALSO GOING TO READ OUT THE RESULT NOW SO WAIT A BI
360    DO
370   Y=Y+1
380    WHILE Y<200
390    GOTO 680 :  REM GOTO THE RESULT ROUTINE
400    GOTO 120 :  REM GOTO THE CLEANUP ROUTINE
410    REM COLLECT
420    CLEAR S
430    PUSH 2 :  REM COLLECT IS 0010
440    PUSH 201
450    CALL 25
460    CALL 54
470    GOTO 120 :  REM GOTO THE CLEANUP ROUTINE
480    REM REFUND
490    CLEAR S
500    PUSH 4 :  REM RETURN IS 0100
510    PUSH 201
520    CALL 25
530    CALL 54
540    GOTO 120 :  REM GOTO THE CLEANUP ROUTINE
550    REM OPERATOR ATTACHED
560    CLEAR S
570    LET O=1 :  REM SET THE VAR SO THE CLEANUP ROUTINE KNOWS
580    PUSH 8 :  REM OPER ATTACHED IS 1000
590    PUSH 201
600    CALL 25
610    CALL 54
620    GOTO 120 :  REM GOTO THE CLEANUP ROUTINE
630    REM OPERATOR DETATCHED
640    CLEAR S
650    LET O=0 :  REM SET VAR TO 0 THE CLEANUP ROUTINE WILL TURN THE RELAY OFF
660    GOTO 170 :  REM GOTO THE CLEANUP ROUTINE SKIPPING THE DELAY
670    REM RESULT READOUT
680    CLEAR S
690    CALL 53
700    PUSH 201
710    CALL 15
720    POP W
730    PRINT W
740    GOTO 120 :  REM GOTO THE CLEANUP ROUTINE

